// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16], reset, load, inc;
    OUT out[16];
    
    PARTS:
    // Always load values into the register
    Register(in=resetMux, load=true, out=out, out=regOut);
    Inc16(in=regOut, out=incOut);

    /*
    reset overrides everything
    load overrides inc
    so we arrange the mux in this order: inc --> load --> reset
    */
    Mux16(a=regOut, b=incOut, sel=inc, out=incMux);
    Mux16(a=incMux, b=in, sel=load, out=loadMux);
    Mux16(a=loadMux, b=false, sel=reset, out=resetMux);
}