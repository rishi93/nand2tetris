// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/Memory.hdl
/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O.
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load=1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid and reads 0. Access to any address
 * in the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the Screen
 * and Keyboard chip specifications given in the lectures and the book.
 */
CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    // Send load to the right device
    DMux(in=load, sel=address[14], a=ramLoad, b=peripheralsLoad);
    DMux(in=peripheralsLoad, sel=address[13], a=screenLoad, b=keyboardLoad);

    // Access the RAM
	RAM16K(in=in, load=ramLoad, address=address[0..13], out=ramOut);
    // Access the Screen
    Screen(in=in, load=screenLoad, address=address[0..12], out=screenOut);
    // Access the Keyboard
    Keyboard(out=keyboardOut);
    // Make sure all the other address bits are 0
    // address[0..13] must be all 0, then it accesses the keyboard
    // else it is an invalid address
    Or8Way(in=address[0..7], out=addressHalf1);
    Or8Way(in[0..4]=address[8..12], in[5..7]=false, out=addressHalf2);
    Or(a=addressHalf1, b=addressHalf2, out=addressFull);

    // If all the address bits for keyboard are 0, then output keyboard
    // else it's outside the valid range, output 0
    Mux16(a=keyboardOut, b=false, sel=addressFull, out=keyboardOutFinal);
    // Output keyboard or screen
    Mux16(a=screenOut, b=keyboardOutFinal, sel=address[13], out=screenAndKeyboard);
    Mux16(a=ramOut, b=screenAndKeyboard, sel=address[14], out=out);
    

}