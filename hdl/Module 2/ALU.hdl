// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // Preprocessing of x
    Mux16(a=x, b=false, sel=zx, out=zxOut);
    Not16(in=zxOut, out=nxOut);
    Mux16(a=zxOut, b=nxOut, sel=nx, out=xOut);

    // Preprocessing of y
    Mux16(a=y, b=false, sel=zy, out=zyOut);
    Not16(in=zyOut, out=nyOut);
    Mux16(a=zyOut, b=nyOut, sel=ny, out=yOut);

    // Processing of f (function)
    And16(a=xOut, b=yOut, out=xAndY);
    Add16(a=xOut, b=yOut, out=xAddY);
    Mux16(a=xAndY, b=xAddY, sel=f, out=fOut);

    // Processing of no (not out)
    Not16(in=fOut, out=noOut);

    // Final output
    Mux16(a=fOut, b=noOut, sel=no, out=out, out[0..7]=zrIn1, out[8..15]=zrIn2, out[15]=ng);

    // other output bits
    Or8Way(in=zrIn1, out=zr1);
    Or8Way(in=zrIn2, out=zr2);
    Or(a=zr1, b=zr2, out=zrNot);
    Not(in=zrNot, out=zr);
}